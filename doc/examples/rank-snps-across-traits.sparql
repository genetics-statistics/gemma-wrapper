
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX gn: <http://genenetwork.org/id/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX gnc: <http://genenetwork.org/category/>
PREFIX gnt: <http://genenetwork.org/term/>
PREFIX sdmx-measure: <http://purl.org/linked-data/sdmx/2009/measure#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX xkos: <http://rdf-vocabulary.ddialliance.org/xkos#>
PREFIX pubmed: <http://rdf.ncbi.nlm.nih.gov/pubmed/>

SELECT ?name ?chr ?pos ?traits FROM <http://pan-test.genenetwork.org> WHERE {
  {
    SELECT ?marker (count(?traitid) as ?traits) WHERE {
      ?snp a gnt:mappedLocus ;
      gnt:mappedSnp ?trait.
      ?trait gnt:run gn:test ;
      gnt:traitId ?traitid .
      ?snp gnt:locus ?marker .
    } GROUP BY ?marker
  }
  FILTER (?traits > 30) .
  ?marker gnt:pos ?pos ;
  gnt:chr ?chr ;
  rdfs:label ?name.
  FILTER(contains(?name,"Marker") && ?pos < 10000) # because of duplicates in DB
} ORDER BY DESC(?traits)
